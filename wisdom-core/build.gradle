import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/TrustedDataFramework/java-rlp")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        maven { url "http://repo.springsource.org/plugins-release" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
        classpath 'com.typesafe:config:1.2.1'
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }

}

plugins {
    id 'application'
//    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.0'
    id 'org.ajoberstar.grgit' version '2.2.0' apply false
//    id 'org.springframework.boot' version '2.1.4.RELEASE'
}

apply plugin: 'propdeps-maven'
apply plugin: 'com.jfrog.artifactory-upload'
apply plugin: 'docker'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

processResources{
    exclude('genesis/wisdom-genesis-generator.json')
}

//apply plugin: 'io.spring.dependency-management'


sourceCompatibility = 1.8

mainClassName = 'org.wisdom.Start'
applicationDefaultJvmArgs = ["-server", "-Xss2m", "-Xmx2G", "-XX:-OmitStackTraceInFastThrow"]

if (project.hasProperty('jvmArgs')) {
    applicationDefaultJvmArgs = applicationDefaultJvmArgs + project.jvmArgs.split('\\s+').toList()
}
if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
}


ext.generatedSrcDir = file('src/gen/java')

sourceSets.main.java.srcDirs += generatedSrcDir


tasks.withType(JavaCompile) {
    options.warnings = false
}

task runKeyStoreTool(type: JavaExec){
    main = "org.wisdom.tools.KeyStoreTool"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    if (project.hasProperty("appArgs")) {
        args appArgs.split(" ")
    }
}

task runTransactionTestTool(type: JavaExec){
    main = "org.wisdom.tools.TransactionTestTool"
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty("appArgs")) {
        args appArgs.split(" ")
    }
}

// bash src/main/docker/core_v2/build-docker.sh -t test
task buildDockerJdk(type: Docker) {
    push = false
    tag = "wisdomchain/server_jre8"
    tagVersion = 'latest'
    applicationName = 'server_jre8'
    dockerfile = file('src/main/docker/server_jre8/Dockerfile')
}

// bash src/main/docker/server_jre8_with_dependencies/build-docker.sh -t latest
task buildDockerJdkWithDependencies(type: Docker) {
    push = false
    tag = "wisdomchain/server_jre8_with_dependencies"
    tagVersion = 'latest'
    applicationName = 'server_jre8_with_dependencies'
    dockerfile = file('src/main/docker/server_jre8_with_dependencies/Dockerfile')
    doFirst {
        copy {
            from copyDependencies into "${stageDir}/lib"
        }
        copy{
            from 'src/main/resources/genesis/wisdom-genesis-generator.json' into "${stageDir}/genesis"
        }
    }
}

task buildDockerCore(type: Docker, dependsOn: jar) {
    push = false
    tag = "wisdomchain/wdc_core"
    tagVersion = 'latest'
    applicationName = 'wdc_core'
    dockerfile = file('src/main/docker/core/Dockerfile')

    doFirst {
        copy {
            from jar into stageDir
        }
        copy {
            from 'src/main/docker/core/run_wdc_core.sh' into stageDir
        }
        copy {
            from 'src/main/docker/core/version.json' into stageDir
        }
    }
}

task buildDockerCoreTest(type: Docker, dependsOn: jar) {
    push = false
    tag = "wisdomchain/wdc_core"
    tagVersion = 'test'
    applicationName = 'wdc_core'
    dockerfile = file('src/main/docker/core/Dockerfile')

    doFirst {
        copy {
            from jar into stageDir
        }
        copy {
            from 'src/main/docker/core/run_wdc_core.sh' into stageDir
        }
        copy {
            from 'src/main/docker/core/version.json' into stageDir
        }
    }
}

task buildDockerFullNode(type: Docker, dependsOn: jar) {
    push = false
    tag = "wisdomchain/wisdom_fullnode"
    tagVersion = 'latest'
    applicationName = 'wdc_fullnode'
    dockerfile = file('src/main/docker/core/Dockerfile')

    doFirst {
        copy {
            from jar into stageDir
        }
        copy {
            from copyDependencies into "${stageDir}/lib"
        }
        copy {
            from 'src/main/docker/core/run_wdc_core.sh' into stageDir
        }
    }
}

task buildDockerPgsql(type: Docker) {
    push = false
    tag = "wisdomchain/wdc_pgsql"
    tagVersion = 'latest'
    applicationName = 'wdc_pgsql'
    dockerfile = file('src/main/docker/pgsql/Dockerfile')

    doFirst {
        copy {
            from 'src/main/resources/ddl.sql' into stageDir
        }
        copy {
            from 'src/main/docker/pgsql/init.sh' into stageDir
        }
    }
}

/**
 * Runs all tests
 * EthereumJ also runs common Ethereum clients tests
 * See {@link #prepareGithubTests()} if you want to use local repo
 * instead of downloading tests during test execution
 */
test {

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }

    jvmArgs = ["-Xss8m", "-Xmx3G"]

    prepareGithubTests()
    // If Github tests directory is provided, it's passed to test runner
    if (System.getProperty("GitHubTests.testPath") != null) {
        jvmArgs.add('-DGitHubTests.testPath' + '=' + System.getProperty("GitHubTests.testPath"))
    }

    testLogging {
        events "failed"
        exceptionFormat "short"
    }

    systemProperties System.properties
    systemProperty "user.dir", workingDir
    systemProperty "file.encoding", "UTF-8"
}

ext {
    slf4jVersion = '1.7.7'
    leveldbVersion = '0.7'
    scastleVersion = '1.58.0.0'
    springVersion = '4.3.19.RELEASE'
    hibernateVersion = '4.3.7.Final'
    junitVersion = '4.11'
    springbootVersion = '2.1.0.RELEASE'
    guavaVersion = '28.1-jre'
    gRPCVersion = '1.25.0'
    lombokVersion = '1.18.10'
    commonsCliVersion = '1.4'
    math3Version = '3.6'
    commonsLang3Version = '3.4'
    leveldbJNIVersion = '1.18.3'
    jargon2Version = '1.1.1'
    bouncycastleVersion = '1.61'
    commonsCollections4Version = '4.0'
}

dependencies {

    compile "com.google.guava:guava:${guavaVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:2.5.1"
    compile "org.apache.commons:commons-collections4:${commonsCollections4Version}"
    compile "org.apache.commons:commons-lang3:${commonsLang3Version}"
    compile "org.apache.commons:commons-math3:${math3Version}"
    compile "commons-codec:commons-codec:1.10"
    compile "org.springframework:spring-context:${springVersion}"
    compile "com.typesafe:config:1.2.1"
    compile("com.googlecode.json-simple:json-simple:1.1.1") {
        exclude group: 'junit', module: 'junit'
    }


    // used to hide spring initialization logs messages in samples
    optional "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    compile "junit:junit:${junitVersion}"
    compile "org.springframework:spring-test:${springVersion}"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.github.briandilley.jsonrpc4j:jsonrpc4j:1.1"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    compile "org.springframework.boot:spring-boot-starter-web:${springbootVersion}"

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    compile "org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}"
    // https://mvnrepository.com/artifact/com.kosprov.jargon2/jargon2-api
    compile "com.kosprov.jargon2:jargon2-api:${jargon2Version}"
    // https://mvnrepository.com/artifact/com.kosprov.jargon2/jargon2-native-ri-backend
    compile "com.kosprov.jargon2:jargon2-native-ri-backend:${jargon2Version}"
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.6.1'
    // https://mvnrepository.com/artifact/net.sf.json-lib/json-lib
    compile('net.sf.json-lib:json-lib:2.2.3:jdk15')
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testCompile "org.springframework.boot:spring-boot-starter-test:${springbootVersion}"

    // https://mvnrepository.com/artifact/com.alibaba/fastjson
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.47'
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    // https://mvnrepository.com/artifact/com.googlecode.concurrentlinkedhashmap/concurrentlinkedhashmap-lru
    compile group: 'com.googlecode.concurrentlinkedhashmap', name: 'concurrentlinkedhashmap-lru', version: '1.4.2'
//     https://mvnrepository.com/artifact/javax.validation/validation-api
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile "org.springframework.boot:spring-boot-starter-jdbc:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-logging:${springbootVersion}"
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.6.0'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.17.Final'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'

    compile group: 'io.netty', name: 'netty-all', version: '4.1.42.Final'
    compile "io.grpc:grpc-netty-shaded:${gRPCVersion}"
    compile "io.grpc:grpc-protobuf:${gRPCVersion}"
    compile "io.grpc:grpc-stub:${gRPCVersion}"
    // https://mvnrepository.com/artifact/org.iq80.leveldb/leveldb
    compile "org.iq80.leveldb:leveldb:${leveldbVersion}"
    compile "org.ethereum:leveldbjni-all:${leveldbJNIVersion}"

    compile group: 'org.quartz-scheduler', name: 'quartz',version: '2.2.3'
    compile "commons-cli:commons-cli:${commonsCliVersion}"
    compile group: 'com.beust', name: 'jcommander', version: '1.72'
    compile group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.10.5'
    compile group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.10.5'
    compile group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.10.5'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"


    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"



    compile fileTree(dir:'libs',include:['*.jar'])
    compile "org.springframework.boot:spring-boot-starter-actuator:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-security:${springbootVersion}"

    compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'
}


javadoc {
    options.author = true
    options.header = project.name
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
    options.links(
            "http://docs.oracle.com/javase/8/docs/api/",
            "http://netty.io/4.0/api/"
    )
}


task copyDependencies(type: Copy) {
    from configurations.runtime
    destinationDir = file('build/libs/lib')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': '. ' + configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
}

// jar.dependsOn += copyDependencies


//create a single Jar with all dependencies
task fatJar(type: Jar) {
    classifier = 'all'
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

//jacocoTestReport {
//    reports {
//        xml.enabled = true
//        html.enabled = true
//    }
//}

def bintrayUsername = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
def bintrayPassword = project.hasProperty('bintrayKey') ? project.bintrayKey : ''
def pullRequest = System.getenv('TRAVIS_PULL_REQUEST')
def branchName = System.getenv('TRAVIS_BRANCH')

artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUsername
            password = bintrayPassword
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }

}

artifactoryPublish.onlyIf {
    (branchName != null) &&
            (!pullRequest || pullRequest == 'false') &&
            project.version.endsWith('-SNAPSHOT') &&
            (branchName.startsWith('master') ||
                    branchName.startsWith('develop') ||
                    branchName.startsWith('stage'))
}

bintrayUpload.onlyIf {
    // comment 'false' and uncomment the next line to publish next release
    (!pullRequest || pullRequest == 'false') && !project.version.endsWith('-SNAPSHOT')
//     false
}

tasks.processResources.doLast() {
    println 'This will be printed after the build task even if something else calls the build task'

    File versionfile = file(new File('build/resources/main/build-info.properties'))
    versionfile.text = 'build.hash=' + gitCommitHash() + '\n' + 'build.time=' + buildTime() + '\n' + 'build.branch=' + gitCurBranch() + '\n'
}

task publish {

    println "publishing if master || develop current branch: " + branchName
    description = "Publishes snapshots to oss.jfrog.org and releases to Bintray/JCenter"
    dependsOn artifactoryPublish, bintrayUpload
}

def gitCommitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("[dd/MMM/yyyy]-[HH:mm]")
    df.setTimeZone(TimeZone.getTimeZone("GMT"));
    return '[GMT]-' + df.format(new Date())
}

def gitCurBranch() {
    def branchName = System.getenv('TRAVIS_BRANCH')
    if (branchName) return branchName
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    return process.text.trim()
}


import org.ajoberstar.grgit.Grgit
import com.typesafe.config.Config
import com.typesafe.config.ConfigFactory

def prepareGithubTests() {
    String testsPathKey = "GitHubTests.testPath"
    String testsCommitKey = "GitHubTests.commit"
    String testUserConf = "wisdom-core/src/test/resources/test-user.conf"
    String githubTestsProp = "wisdom-core/src/test/resources/github-tests.prop"
    def repoPath = System.getProperty(testsPathKey)
    if (repoPath == null) {
        try {
            Config userTest = ConfigFactory.parseFile(new File(testUserConf))
            repoPath = userTest.getString(testsPathKey)
        } catch (Exception ex) {
        }
    }
    if (repoPath == null) return
    def commit = System.getProperty(testsCommitKey)
    if (commit == null) {
        Properties testProp = new Properties();
        testProp.load(new File(githubTestsProp).newDataInputStream());
        commit = testProp.getProperty(testsCommitKey)
    }
    if (repoPath != null && commit != null) {
        println "Tests: Checking out Ethereum Tests repo at " + repoPath + " to commit #" + commit
        try {
            def testsRepo = Grgit.open(dir: repoPath)
            testsRepo.fetch()
            testsRepo.checkout(branch: commit)
        } catch (Exception ex) {
            println "Error occurs while trying to checkout local Ethereum tests repo to specific commit"
            println "Either remove " + testsPathKey + " property to use remote repo in tests or fix the issue"
            println "Error: "
            println ex
            throw ex
        }
    }
}
