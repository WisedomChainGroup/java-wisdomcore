import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
        classpath 'com.typesafe:config:1.2.1'
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

plugins {
    id 'application'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.bintray' version '1.0'
    id 'org.ajoberstar.grgit' version '2.2.0' apply false
//    id 'org.springframework.boot' version '2.1.4.RELEASE'
}

apply plugin: 'propdeps-maven'
apply plugin: 'com.jfrog.artifactory-upload'
apply plugin: 'docker'


//apply plugin: 'io.spring.dependency-management'


sourceCompatibility = 1.8

mainClassName = 'org.wisdom.Start'
applicationDefaultJvmArgs = ["-server", "-Xss2m", "-Xmx2G", "-XX:-OmitStackTraceInFastThrow"]

if (project.hasProperty('jvmArgs')) {
    applicationDefaultJvmArgs = applicationDefaultJvmArgs + project.jvmArgs.split('\\s+').toList()
}
if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
}


ext.generatedSrcDir = file('src/gen/java')

sourceSets.main.java.srcDirs += generatedSrcDir


tasks.withType(JavaCompile) {
    options.warnings = false
}

task runRopsten(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = applicationDefaultJvmArgs + '-Dethereumj.conf.res=ropsten.conf'
}

task runTest(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = applicationDefaultJvmArgs + '-Dethereumj.conf.res=test.conf'
}

/**
 *  This is TCK test command line option,
 *  to run the test:
 *     gradle tckRun -PfilePath=[ file path]
 *      e.g: gradle tckRun -PfilePath=E:\temp\test-case.json
 */
task tckRun(type: JavaExec) {

    if (project.hasProperty("filePath")) {
        args = ["filerun", "$filePath"]
        jvmArgs = ["-Xss4M"]
    }

    if (project.hasProperty("content")) {
        args = ["content", "$content"]
        jvmArgs = ["-Xss4M"]
    }

    main = "org.ethereum.tck.RunTck"
    classpath = sourceSets.test.runtimeClasspath
}

task runCoreUser(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    println "++++++++++++++ " + System.getProperty('ethereumj.conf.file')
    if (System.getProperty('ethereumj.conf.file') != null) {
        def confPathProperty = "-Dethereumj.conf.file=" + System.getProperty('ethereumj.conf.file')
        jvmArgs = applicationDefaultJvmArgs + confPathProperty
    }
    if (System.getProperty('spring.datasource.password') != null) {
        def dbpassword = "-Dspring.datasource.password=" + System.getProperty('spring.datasource.password')
        jvmArgs = jvmArgs + dbpassword
    }
    if (System.getProperty('spring.datasource.username') != null) {
        def dbusername = "-Dspring.datasource.username=" + System.getProperty('spring.datasource.username')
        jvmArgs = jvmArgs + dbusername
    }
    println jvmArgs
}

task buildDockerJdk(type: Docker) {
    push = false
    tag = "wisdomchain/server_jre8"
    tagVersion = 'latest'
    applicationName = 'server_jre8'
    dockerfile = file('src/main/docker/server_jre8/Dockerfile')
}


task buildDockerCore(type: Docker, dependsOn: jar) {
    push = false
    tag = "wisdomchain/wdc_core"
    tagVersion = 'latest'
    applicationName = 'wdc_core'
    dockerfile = file('src/main/docker/core/Dockerfile')

    doFirst {
        copy {
            from jar into stageDir
        }
        copy {
            from copyDependencies into "${stageDir}/lib"
        }
        copy {
            from 'src/main/docker/core/run_wdc_core.sh' into stageDir
        }
        copy {
            from 'src/main/docker/core/version.json' into stageDir
        }
    }
}

task buildDockerFullNode(type: Docker, dependsOn: jar) {
    push = false
    tag = "wisdomchain/wisdom_fullnode"
    tagVersion = 'latest'
    applicationName = 'wdc_fullnode'
    dockerfile = file('src/main/docker/core/Dockerfile')

    doFirst {
        copy {
            from jar into stageDir
        }
        copy {
            from copyDependencies into "${stageDir}/lib"
        }
        copy {
            from 'src/main/docker/core/run_wdc_core.sh' into stageDir
        }
    }
}

task buildDockerPgsql(type: Docker) {
    push = false
    tag = "wisdomchain/wdc_pgsql"
    tagVersion = 'latest'
    applicationName = 'wdc_pgsql'
    dockerfile = file('src/main/docker/pgsql/Dockerfile')

    doFirst {
        copy {
            from 'src/main/resources/ddl.sql' into stageDir
        }
        copy {
            from 'src/main/docker/pgsql/init.sh' into stageDir
        }
    }
}

/**
 * Runs all tests
 * EthereumJ also runs common Ethereum clients tests
 * See {@link #prepareGithubTests()} if you want to use local repo
 * instead of downloading tests during test execution
 */
test {

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }

    jvmArgs = ["-Xss8m", "-Xmx3G"]

    prepareGithubTests()
    // If Github tests directory is provided, it's passed to test runner
    if (System.getProperty("GitHubTests.testPath") != null) {
        jvmArgs.add('-DGitHubTests.testPath' + '=' + System.getProperty("GitHubTests.testPath"))
    }

    testLogging {
        events "failed"
        exceptionFormat "short"
    }

    systemProperties System.properties
    systemProperty "user.dir", workingDir
    systemProperty "file.encoding", "UTF-8"
}

ext {
    slf4jVersion = '1.7.7'
    leveldbVersion = '0.7'
    scastleVersion = '1.58.0.0'
    springVersion = '4.3.19.RELEASE'
    hibernateVersion = '4.3.7.Final'
    junitVersion = '4.11'
    springbootVersion = '2.1.0.RELEASE'
}

dependencies {

    compile "com.google.guava:guava:24.1-jre"
    compile group: 'io.netty', name: 'netty-all', version: '4.1.30.Final'
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:2.5.1"
    compile "org.apache.commons:commons-collections4:4.0"
    compile "org.apache.commons:commons-lang3:3.4"
    compile 'org.apache.commons:commons-math3:3.6'
    compile "commons-codec:commons-codec:1.10"
    compile "org.springframework:spring-context:${springVersion}"
    compile "com.typesafe:config:1.2.1"
    compile("com.googlecode.json-simple:json-simple:1.1.1") {
        exclude group: 'junit', module: 'junit'
    }


    // used to hide spring initialization logs messages in samples
    optional "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.github.briandilley.jsonrpc4j:jsonrpc4j:1.1"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    compile "org.springframework.boot:spring-boot-starter-web:${springbootVersion}"

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.61'
    // https://mvnrepository.com/artifact/com.kosprov.jargon2/jargon2-api
    compile group: 'com.kosprov.jargon2', name: 'jargon2-api', version: '1.1.1'
    // https://mvnrepository.com/artifact/com.kosprov.jargon2/jargon2-native-ri-backend
    compile group: 'com.kosprov.jargon2', name: 'jargon2-native-ri-backend', version: '1.1.1'
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.6.1'
    // https://mvnrepository.com/artifact/net.sf.json-lib/json-lib
    compile('net.sf.json-lib:json-lib:2.2.3:jdk15')
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testCompile "org.springframework.boot:spring-boot-starter-test:${springbootVersion}"

    // https://mvnrepository.com/artifact/com.alibaba/fastjson
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.47'
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    // https://mvnrepository.com/artifact/com.googlecode.concurrentlinkedhashmap/concurrentlinkedhashmap-lru
    compile group: 'com.googlecode.concurrentlinkedhashmap', name: 'concurrentlinkedhashmap-lru', version: '1.4.2'
//     https://mvnrepository.com/artifact/javax.validation/validation-api
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile "org.springframework.boot:spring-boot-starter-jdbc:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-logging:${springbootVersion}"
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.6.0'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.17.Final'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'

    compile 'io.grpc:grpc-netty-shaded:1.22.1'
    compile 'io.grpc:grpc-protobuf:1.22.1'
    compile 'io.grpc:grpc-stub:1.22.1'

    compile group: 'org.rocksdb', name: 'rocksdbjni', version: '6.0.1'
}


javadoc {
    options.author = true
    options.header = project.name
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
    options.links(
            "http://docs.oracle.com/javase/8/docs/api/",
            "http://netty.io/4.0/api/"
    )
}


task copyDependencies(type: Copy) {
    from configurations.runtime
    destinationDir = file('build/libs/lib')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': '. ' + configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
}
jar.dependsOn += copyDependencies


//create a single Jar with all dependencies
task fatJar(type: Jar) {
    classifier = 'all'
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

def bintrayUsername = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
def bintrayPassword = project.hasProperty('bintrayKey') ? project.bintrayKey : ''
def pullRequest = System.getenv('TRAVIS_PULL_REQUEST')
def branchName = System.getenv('TRAVIS_BRANCH')

artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUsername
            password = bintrayPassword
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }

}

artifactoryPublish.onlyIf {
    (branchName != null) &&
            (!pullRequest || pullRequest == 'false') &&
            project.version.endsWith('-SNAPSHOT') &&
            (branchName.startsWith('master') ||
                    branchName.startsWith('develop') ||
                    branchName.startsWith('stage'))
}

bintray {
    user = bintrayUsername
    key = bintrayPassword

    configurations = ['archives']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        userOrg = 'ethereum' //An optional organization name when the repo belongs to one of the user's orgs
        repo = 'maven'
        name = 'org.ethereum'
    }
}

bintrayUpload.onlyIf {
    // comment 'false' and uncomment the next line to publish next release
    (!pullRequest || pullRequest == 'false') && !project.version.endsWith('-SNAPSHOT')
//     false
}

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

tasks.processResources.doLast() {
    println 'This will be printed after the build task even if something else calls the build task'

    File versionfile = file(new File('build/resources/main/build-info.properties'))
    versionfile.text = 'build.hash=' + gitCommitHash() + '\n' + 'build.time=' + buildTime() + '\n' + 'build.branch=' + gitCurBranch() + '\n'
}

task publish {

    println "publishing if master || develop current branch: " + branchName
    description = "Publishes snapshots to oss.jfrog.org and releases to Bintray/JCenter"
    dependsOn artifactoryPublish, bintrayUpload
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // sort to make pom dependencies order consistent to ease comparison of older poms
        generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
            "$dep.scope:$dep.groupId:$dep.artifactId"
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = "ethereumj"
            description = "Java implementation of the Ethereum protocol"
            url = "https://github.com/ethereum/ethereumj"
            organization {
                name = "Ethereum"
                url = "https://ethereum.org"
            }
            licenses {
                license {
                    name "GNU Lesser General Public License v3.0"
                    url "https://www.gnu.org/licenses/lgpl-3.0.en.html"
                    distribution "repo"
                }
            }
            scm {
                url = "https://github.com/ethereum/ethereumj"
                connection = "scm:git:git://github.com/ethereum/ethereumj"
                developerConnection = "scm:git:git://github.com/ethereum/ethereumj"
            }
            developers {
                developer {
                    id = "rmandeleil"
                    name = "Roman Mandeleil"
                    email = "roman.mandeleil@gmail.com"
                }

                developer {
                    id = "Nashatyrev"
                    name = "Anton Nashatyrev"
                    email = "Anton.nashatyrev@gmail.com"
                }

                developer {
                    id = "zilm13"
                    name = "Dmitry Shmatko"
                    email = "zilm@zilm.net"
                }

                developer {
                    id = "mkalinin"
                    name = "Mikhail Kalinin"
                    email = "noblesse.knight@gmail.com"
                }
            }
            issueManagement {
                system = "GitHub Issues"
                url = "https://github.com/ethereum/ethereumj/issues"
            }
        }
    }
}


def gitCommitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("[dd/MMM/yyyy]-[HH:mm]")
    df.setTimeZone(TimeZone.getTimeZone("GMT"));
    return '[GMT]-' + df.format(new Date())
}

def gitCurBranch() {
    def branchName = System.getenv('TRAVIS_BRANCH')
    if (branchName) return branchName
    def process = "git rev-parse --abbrev-ref HEAD".execute()
    return process.text.trim()
}

/**
 * This method is used to prepare local Ethereum Github tests for EthereumJ tests
 *
 * These tests  are designed for all Ethereum clients and
 * maintained by Ethereum Foundation/Community.
 * EthereumJ executes all of them but by default downloads every
 * single file using internet connection.
 * Alternatively you could use local repo to run tests from it.
 *
 * Common usage:
 * 1) Local run:
 *  a. Clone Ethereum tests repo (https://github.com/ethereum/tests)
 *    to some local directory
 *  b. Create `test-user.conf` file in test resources directory.
 *    This file is under `.gitignore`, so it's your local file only.
 *    You could override any EthereumJ configuration in it, but
 *    for using local Ethereum tests repo you need following line:
 *    GitHubTests.testPath=/some/path/with/Ethereum/tests/repo
 *  c. Run `./gradlew test` task.
 * 2) CI run:
 *  a. Clone Ethereum tests repo (https://github.com/ethereum/tests)
 *    to some local directory
 *  b. Run `test` task with following parameter:
 *    -DGitHubTests.testPath=/some/path/with/Ethereum/tests/repo
 *
 * In both cases this method will get commit hash from
 * `src/test/resources/github-tests.prop` and checkout local repo
 * to specific commit. So, EthereumJ tests will run common
 * Ethereum client tests using local copy of repository.
 *
 * Repo with tests is changed often and EthereumJ is guaranteed
 * to pass only tests from specific commit.
 */
import org.ajoberstar.grgit.Grgit
import com.typesafe.config.Config
import com.typesafe.config.ConfigFactory

def prepareGithubTests() {
    String testsPathKey = "GitHubTests.testPath"
    String testsCommitKey = "GitHubTests.commit"
    String testUserConf = "wisdom-core/src/test/resources/test-user.conf"
    String githubTestsProp = "wisdom-core/src/test/resources/github-tests.prop"
    def repoPath = System.getProperty(testsPathKey)
    if (repoPath == null) {
        try {
            Config userTest = ConfigFactory.parseFile(new File(testUserConf))
            repoPath = userTest.getString(testsPathKey)
        } catch (Exception ex) {
        }
    }
    if (repoPath == null) return
    def commit = System.getProperty(testsCommitKey)
    if (commit == null) {
        Properties testProp = new Properties();
        testProp.load(new File(githubTestsProp).newDataInputStream());
        commit = testProp.getProperty(testsCommitKey)
    }
    if (repoPath != null && commit != null) {
        println "Tests: Checking out Ethereum Tests repo at " + repoPath + " to commit #" + commit
        try {
            def testsRepo = Grgit.open(dir: repoPath)
            testsRepo.fetch()
            testsRepo.checkout(branch: commit)
        } catch (Exception ex) {
            println "Error occurs while trying to checkout local Ethereum tests repo to specific commit"
            println "Either remove " + testsPathKey + " property to use remote repo in tests or fix the issue"
            println "Error: "
            println ex
            throw ex
        }
    }
}
